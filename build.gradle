def commonModule = subprojects.find { it.name == 'carp.common' }
def coreModules = subprojects.findAll { it.name.endsWith( '.core' ) }
def detektModule = subprojects.find { it.name == 'carp.detekt' }


// Load plugin dependencies.
buildscript {
    ext {
        // Version used for all submodule artifacts.
        // Snapshot publishing changes (or adds) the suffix after '-' with 'SNAPSHOT' prior to publishing.
        // The 'publishSigned' task publishes to SonaType's staging repo and 'publishSnapshot' instantly uploads to the snapshots repo.
        globalVersion = '1.0.0-alpha.34'

        versions = [
            // Kotlin multiplatform versions.
            kotlin:'1.5.21',
            serialization:'1.2.2',
            coroutines:'1.5.1',

            // JVM versions.
            jvmTarget:'1.8',
            jUnit5:'5.7.2',
            dokkaPlugin:'1.5.0',
            reflections:'0.9.12',

            // JS versions.
            nodePlugin:'3.1.0',

            // DevOps versions.
            detektPlugin:'1.17.0',
            detektVerifyImplementation:'1.2.2',
            nexusReleasePlugin:'0.22.0' // TODO: Migrate to `gradle-nexus-publish-plugin`.
        ]
    }

    dependencies {
        // Kotlin plugins.
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"

        // JS plugins.
        classpath "com.github.node-gradle:gradle-node-plugin:${versions.nodePlugin}"

        // JVM plugins.
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokkaPlugin}"

        // DevOps plugins.
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detektPlugin}"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:${versions.nexusReleasePlugin}"
    }

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

// Load dependent properties.
def publishProperties = new Properties()
def publishPropertiesFile = rootProject.file('publish.properties')
if (publishPropertiesFile.exists())
{
    publishProperties.load(new FileInputStream(publishPropertiesFile))
}

// Configure all subprojects as testable, publishable, Kotlin multiplatform projects.
// A `kotlinx.serialization` dependency is added to serialize domain models.
configure( subprojects - detektModule ) {
    version = globalVersion

    repositories {
        mavenCentral()
    }

    // Specify platforms and test frameworks to use.
    apply plugin: 'kotlin-multiplatform'
    apply plugin: 'kotlinx-serialization'
    kotlin {
        jvm {
            compilations.main.kotlinOptions.jvmTarget = versions.jvmTarget
            compilations.test.kotlinOptions.jvmTarget = versions.jvmTarget
        }
        js(LEGACY) {
            binaries.executable()
            browser()
        }

        sourceSets {
            commonMain {
                dependencies {
                    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${versions.serialization}"
                }
            }
            commonTest {
                dependencies {
                    implementation kotlin('test-common')
                    implementation kotlin('test-annotations-common')
                }
            }
            jvmTest {
                dependencies {
                    implementation kotlin('test')
                    implementation kotlin('test-junit5')
                    implementation "org.junit.jupiter:junit-jupiter-api:${versions.jUnit5}"
                    runtimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.jUnit5}"
                }
            }
            jsTest {
                dependencies {
                    implementation kotlin('test-js')
                }
            }
        }
    }

    // JVM test configuration.
    jvmTest {
        useJUnitPlatform()
    }

    // Publish configuration.
    // For signing and publishing to work, a 'publish.properties' file needs to be added to the root containing:
    // The OpenPGP credentials to sign all artifacts:
    // > signing.keyFile=<ABSOLUTE PATH TO THE ASCII-ARMORED KEY FILE>
    // > signing.password=<SECRET>
    // A username and password to upload artifacts to the Sonatype repository:
    // > repository.username=<SONATYPE USERNAME>
    // > repository.password=<SONATYPE PASSWORD>
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.dokka'
    task dokkaJvmJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
        dokkaSourceSets {
            register("jvm") {
                platform.set(org.jetbrains.dokka.Platform.jvm)
                sourceRoots.from(kotlin.sourceSets.getByName("jvmMain").kotlin.srcDirs)
            }
        }
    }
    task javadocJar(type: Jar) {
        group JavaBasePlugin.DOCUMENTATION_GROUP
        description 'Create javadoc jar using Dokka'
        archiveClassifier = "javadoc"
        from dokkaJvmJavadoc
    }
    publishing {
        publications {
            all {
                pom {
                    url = 'https://github.com/cph-cachet/carp.core-kotlin'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/cph-cachet/carp.core-kotlin/blob/master/LICENSE.md'
                        }
                    }
                    developers {
                        developer {
                            id = 'whathecode'
                            name = 'Steven Jeuris'
                            email = 'steven.jeuris@gmail.com'
                            organization = 'CACHET'
                            organizationUrl = 'http://www.cachet.dk/'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/cph-cachet/carp.core-kotlin.git'
                        developerConnection = 'scm:git:ssh://github.com:cph-cachet/carp.core-kotlin.git'
                        url = 'https://github.com/cph-cachet/carp.core-kotlin'
                    }
                }
            }
            jvm {
                artifact javadocJar
            }
        }
        repositories {
            maven {
                name "local"
                url "$buildDir/repository"
            }
            maven {
                name = "sonatype"
                url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2' // Staging repo.
                credentials {
                    username = publishProperties['repository.username']
                    password = publishProperties['repository.password']
                }
            }
        }
    }
    // Configure the 'sign' task to sign all files part of the maven package.
    apply plugin: 'signing'
    signing {
        sign publishing.publications
    }
    task publishSigned {
        doFirst {
            def signingKey = new File(publishProperties['signing.keyFile']).text
            def signingPassword = publishProperties['signing.password']
            signing.useInMemoryPgpKeys(signingKey, signingPassword)
        }
    }
    publishSigned.finalizedBy publish
    task publishSnapshot {
        doFirst {
            // Since project dependencies trigger publications of other projects, immediately modify versions of all subprojects.
            (rootProject.subprojects - detektModule).each { project ->
                def versionSplit = project.version.split('-')
                def snapshotVersion = "${versionSplit[0]}-SNAPSHOT"
                project.version = snapshotVersion

                // Override 'maven-publish' publication versions and change staging repo URL to snapshot repo.
                project.publishing.publications.all { version = version }
                project.publishing.repositories.sonatype.url = 'https://oss.sonatype.org/content/repositories/snapshots'
            }
        }
    }
    publishSnapshot.finalizedBy publishSigned
}
// Add 'closeAndReleaseRepository' task to close and release uploads to Sonatype Nexus Repository after 'publishSigned'.
apply plugin: 'io.codearte.nexus-staging'
nexusStaging {
    packageGroup = 'dk.cachet.carp'
    numberOfRetries = 30
    username = publishProperties['repository.username']
    password = publishProperties['repository.password']
}

// TypeScript ambient declaration verification.
def typescriptFolder = 'typescript-declarations'
apply plugin: 'com.github.node-gradle.node'
task setupTsProject(type: NpmTask) {
    workingDir = file(typescriptFolder)
    args = ['install']
}
task copyTestJsSources(type: Copy, dependsOn: setupTsProject) {
    // Make sure no old imported packages are left behind.
    def importedPackages = file("$rootDir/build/js/packages_imported")
    if (importedPackages.exists()) importedPackages.eachFile { it.delete() }

    // Compile all subprojects for which TypeScript declarations are defined.
    def projects = coreModules + commonModule
    projects.each { dependsOn("${it.name}:jsBrowserDistribution") }

    // Copy compiled sources and dependencies to test project node_modules.
    from "$rootDir/build/js/packages"
    from(importedPackages) {
        eachFile {
            def path = it.path
            if (path == ".visited") return // We don't need this file.

            // Remove intermediate version directory: e.g. "kotlin/1.5.10/kotlin.js"
            it.path = path.replaceFirst(/\d+\.\d+.\d+\//, "")
        }
    }
    into "./$typescriptFolder/node_modules"
}
task compileTs(type: NpmTask, dependsOn: copyTestJsSources) {
    workingDir = file(typescriptFolder)
    args = ['run', 'tsc']
}
task verifyTsDeclarations(type: NodeTask, dependsOn: compileTs) {
    script = file("${typescriptFolder}/node_modules/mocha/bin/mocha")
    execOverrides {
        it.workingDir = typescriptFolder
    }
    args = [
        '--require', 'ts-node/register',
        '--require', 'jsdom-global/register',
        './tests/**/*.ts'
    ]
}

// Add common dependencies and configuration:
// - `carp.test` for testing
// - reflection for testing on JVM
// TODO: unknown polymorphic serializers are only expected to be used on the server-side. Can this feature be made optional or refactored?
configure( coreModules + commonModule ) {
    buildscript {
        repositories {
            mavenCentral()
        }

        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
        }
    }

    kotlin {
        sourceSets {
            commonTest {
                dependencies {
                    implementation project(':carp.test')
                }
            }
            jvmTest {
                dependencies {
                    implementation kotlin('reflect')
                    implementation "org.reflections:reflections:${versions.reflections}"
                }
            }

            all {
                // We do not mind being early adopters of Jetbrains APIs likely to change in the future.
                languageSettings.useExperimentalAnnotation("kotlin.RequiresOptIn")
            }
        }
    }
}


// Add dependencies of all core modules on `carp.common`.
configure( coreModules ) {
    kotlin {
        sourceSets {
            commonMain {
                dependencies {
                    api project(':carp.common')
                }
            }
            commonTest {
                dependencies {
                    implementation project(':carp.common.test')
                }
            }
        }
    }
}


// Add code analysis.
configure( rootProject )
{
    repositories {
        mavenCentral()
    }

    apply plugin: 'io.gitlab.arturbosch.detekt'
    detekt {
        dependencies {
            detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${versions.detektPlugin}"
            detektPlugins project(":carp.detekt") // Add custom project-specific rules.
            detektPlugins "dk.cachet.detekt.extensions:detekt-verify-implementation:${versions.detektVerifyImplementation}"
        }
    }
    task detektPasses(type: io.gitlab.arturbosch.detekt.Detekt) {
        source = fileTree("$rootDir")
        {
            include('**/src/**')
            exclude('**/node_modules/**', '**/resources/**')
        }
        config.from("$rootDir/detekt.yml")
        buildUponDefaultConfig = true
        ignoreFailures = false
        def classPaths = project.configurations.getByName("detekt")
        def multiplatformModules = coreModules + commonModule
        multiplatformModules.each { classPaths += it.configurations.getByName("jvmCompileClasspath") }
        classpath.setFrom(classPaths)
    }
    tasks.detekt.jvmTarget = "1.8"
    tasks.detekt.dependsOn ":carp.detekt:assemble" // Ensure 'carp.detekt' is built prior to running code analysis.
}
